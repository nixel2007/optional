#Использовать fluent
#Использовать notify

Перем ХранимыйОбъект;

Процедура ПриСозданииОбъекта(Объект, ДопустимоНеопределено = Ложь)
	ХранимыйОбъект = Объект;
	
	Если НЕ ДопустимоНеопределено Тогда
		ВызватьИсключениеЕслиПустой();
	КонецЕсли;
	
КонецПроцедуры

Функция Пустой() Экспорт
	Возврат ХранимыйОбъект = Неопределено;
КонецФункции

Функция СодержитЗначение() Экспорт
	Возврат НЕ Пустой();
КонецФункции

Функция Обработать(ОписаниеОповещения) Экспорт
	Если Пустой() Тогда
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	ПодготовитьОписаниеОповещенияКВыполнению(ОписаниеОповещения);
	
	РезультатОбработки = Неопределено;
	ОписанияОповещений.ВыполнитьОбработкуОповещения(ОписаниеОповещения, РезультатОбработки);
	
	Возврат Новый Опциональный(РезультатОбработки, Истина);
КонецФункции

Функция Развернуть(ОписаниеОповещения) Экспорт

	ПодготовитьОписаниеОповещенияКВыполнению(ОписаниеОповещения);

	Если Пустой() Тогда
		Возврат Новый Опциональный(Неопределено, Истина);
	КонецЕсли;

	РезультатОбработки = Неопределено;
	ОписанияОповещений.ВыполнитьОбработкуОповещения(ОписаниеОповещения, РезультатОбработки);

	Если ТипЗнч(РезультатОбработки) <> Тип("Опциональный") Тогда
		ВызватьИсключение "Функция развертки не вернула Опциональный";
	КонецЕсли;
	
	Возврат РезультатОбработки;

КонецФункции

Функция Фильтровать(ОписаниеОповещения) Экспорт
	Если Пустой() Тогда
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	ПодготовитьОписаниеОповещенияКВыполнению(ОписаниеОповещения);
	
	РезультатФильтрации = Ложь;
	ОписанияОповещений.ВыполнитьОбработкуОповещения(ОписаниеОповещения, РезультатФильтрации);
	
	Если РезультатФильтрации Тогда
		Возврат ЭтотОбъект;
	Иначе
		Возврат Новый Опциональный(Неопределено, Истина);
	КонецЕсли;
КонецФункции

Процедура ЕслиСодержитЗначение(ОписаниеОповещения) Экспорт
	Если Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьОписаниеОповещенияКВыполнению(ОписаниеОповещения);

	ОписанияОповещений.ВыполнитьОбработкуОповещения(ОписаниеОповещения, ХранимыйОбъект);
	
КонецПроцедуры

Процедура ЕслиСодержитЗначениеИначе(ОписаниеОповещения, ОписаниеОповещенияИначе) Экспорт
	
	ПодготовитьОписаниеОповещенияКВыполнению(ОписаниеОповещения);
	ПодготовитьОписаниеОповещенияКВыполнению(ОписаниеОповещенияИначе);

	Если СодержитЗначение() Тогда
		ОписанияОповещений.ВыполнитьОбработкуОповещения(ОписаниеОповещения, ХранимыйОбъект);
	Иначе
		ОписанияОповещений.ВыполнитьОбработкуОповещения(ОписаниеОповещенияИначе);
	КонецЕсли;

КонецПроцедуры

Функция Или_(ОписаниеОповещения) Экспорт
	Если СодержитЗначение() Тогда
		Возврат ЭтотОбъект;
	КонецЕсли;

	РезультатОбработки = Неопределено;
	ОписанияОповещений.ВыполнитьОбработкуОповещения(ОписаниеОповещения, РезультатОбработки);

	Возврат Новый Опциональный(РезультатОбработки, Истина);

КонецФункции

Функция ПроцессорКоллекций() Экспорт

	Если Пустой() Тогда
		Возврат ПроцессорыКоллекций.ИзНабора();
	КонецЕсли;

	Возврат ПроцессорыКоллекций.ИзНабора(ХранимыйОбъект);

КонецФункции

Функция Получить() Экспорт
	ВызватьИсключениеЕслиПустой();
	Возврат ХранимыйОбъект;
КонецФункции

Функция Иначе_(ЗначениеИначе) Экспорт

	Если СодержитЗначение() Тогда
		Возврат ХранимыйОбъект;
	КонецЕсли;

	Возврат ЗначениеИначе;

КонецФункции

Функция ИначеПолучить(ОписаниеОповещения) Экспорт
	
	Если СодержитЗначение() Тогда
		Возврат ХранимыйОбъект;
	КонецЕсли;

	РезультатОбработки = Неопределено;
	ОписанияОповещений.ВыполнитьОбработкуОповещения(ОписаниеОповещения, РезультатОбработки);

	Возврат РезультатОбработки;

КонецФункции

Функция ИначеВызватьИсключение() Экспорт
	
	ВызватьИсключениеЕслиПустой();
	
	Возврат ХранимыйОбъект;

КонецФункции

Процедура ВызватьИсключениеЕслиПустой()
	Если Пустой() Тогда
		ВызватьИсключение "Хранимое значение является Неопределено";
	КонецЕсли;
КонецПроцедуры

Процедура ПодготовитьОписаниеОповещенияКВыполнению(ОписаниеОповещения)
	Если ОписаниеОповещения.ДополнительныеПараметры = Неопределено Тогда
		ОписаниеОповещения.ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	ОписаниеОповещения.ДополнительныеПараметры.Вставить("Объект", ХранимыйОбъект);
КонецПроцедуры
